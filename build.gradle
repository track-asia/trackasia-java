plugins {
    alias(libs.plugins.gradle.nexus)
    alias(libs.plugins.kotlin.multiplatform) apply false
    alias(libs.plugins.kotlinx.serialization) apply false
    alias(libs.plugins.kotlin.cocoapods) apply false
    alias(libs.plugins.kotlin.dokka)
}

apply from: file("${rootDir}/gradle/artifact-settings.gradle")
apply from: "${rootDir}/gradle/publish-root.gradle"

defaultTasks('clean', 'build')

nexusPublishing {
    repositories {
        sonatype {
            useStaging = !project.ext.isSnapshot
            stagingProfileId = sonatypeStagingProfileId
            username = ossrhUsername
            password = ossrhPassword
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
    transitionCheckOptions {
        maxRetries = 120
        delayBetween = Duration.ofSeconds(10)
    }
}

allprojects {
    project.plugins.withType(org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsPlugin) {
        project.extensions.getByType(org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsEnvSpec).version = "23.6.0"
    }
}

task printVersion {
    doLast {
        println "Version from project: $version"
        println "Version from properties: ${project.findProperty('VERSION')}"
    }
}