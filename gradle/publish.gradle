apply plugin: 'maven-publish'
apply plugin: 'signing'

apply from: file("${rootDir}/gradle/artifact-settings.gradle")
apply from: file("${rootDir}/gradle/publish-root.gradle")

// Define group & version for Kotlin Multiplatform artifacts
group = project.ext.trackAsiaArtifactGroupId
version = project.ext.versionName

tasks.register('dokkaHtmlJar', Jar.class) {
    dependsOn(dokkaHtml)
    from(dokkaHtml)
    archiveClassifier.set("javadoc")
}

publishing {
    publications.withType(MavenPublication) {
        groupId project.ext.trackAsiaArtifactGroupId
        version project.ext.versionName

        // Drop the `services` from artifact ID
        artifactId artifactId.replace("services-", "")

        project.logger.info("Publishing project $artifactId with version: ${version}")

        artifact tasks.dokkaHtmlJar

        pom {
            name = project.ext.trackAsiaArtifactTitle
            description = project.ext.trackAsiaArtifactTitle
            url = project.ext.trackAsiaArtifactUrl
            licenses {
                license {
                    name = project.ext.trackAsiaArtifactLicenseName
                    url = project.ext.trackAsiaArtifactLicenseUrl
                }
            }
            developers {
                developer {
                    id = project.ext.trackAsiaDeveloperId
                    name = project.ext.trackAsiaDeveloperName
                    email = 'team@track-asia.com'
                }
            }
            scm {
                connection = project.ext.trackAsiaArtifactScmUrl
                developerConnection = project.ext.trackAsiaArtifactScmUrl
                url = project.ext.trackAsiaArtifactUrl
            }
        }
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('docencoding', 'UTF-8')
    options.addStringOption('charset', 'UTF-8')
}

signing {
    required { project.ext["signing.keyId"] != null }
    sign publishing.publications
}
